// Copyright 1998-2017 Epic Games, Inc. All Rights Reserved.

#include "Common.ush"
#include "GammaCorrectionCommon.ush"
#include "SlateShaderCommon.ush"

float4x4 ViewProjection;

float SwitchVerticalAxisMultiplier;

VertexToPixelInterpolants Main(
	in float4 InTextureCoordinates : ATTRIBUTE0,
	in float2 InMaterialTextureCoordinates : ATTRIBUTE1,
	in float2 InPosition : ATTRIBUTE2,
	in half4 InColor : ATTRIBUTE3,
    in uint2 PixelSize : ATTRIBUTE4
#if USE_SLATE_INSTANCING
	, in float4 InstanceParam : ATTRIBUTE5
#endif
	)
{
	VertexToPixelInterpolants VOut = (VertexToPixelInterpolants)0;

    float4 WorldPosition = float4(InPosition.xy, 0, 1);

	// save off the original input position value, Line AA will require this
	VOut.OriginalPosition = WorldPosition;

#if SOURCE_IN_LINEAR_SPACE
	InColor.rgb = sRGBToLinear(InColor.rgb);
#endif

	half4 FinalVertexColor = InColor FCOLOR_COMPONENT_SWIZZLE;

	VOut.MaterialTexCoords = InMaterialTextureCoordinates;
	VOut.Color = FinalVertexColor;
	VOut.TextureCoordinates[0] = InTextureCoordinates;

#if USE_MATERIALS
	FMaterialVertexParameters VertexParameters = (FMaterialVertexParameters)0;
	VertexParameters.WorldPosition = WorldPosition.xyz;

	VertexParameters.VertexColor = FinalVertexColor;

#if NUM_MATERIAL_TEXCOORDS_VERTEX

#if !USE_SLATE_INSTANCING
	// pass along local screen space size
	// To look this up in a material you use uv channel 3
	float4 InstanceParam = float4(0,0, PixelSize);
#endif

	float2 UVArrays[8] =
	{
		InTextureCoordinates.xy, //uv channel 0
		InTextureCoordinates.zw, //uv channel 1
		InstanceParam.xy, //uv channel 2
		InstanceParam.zw, //uv channel 3
		InMaterialTextureCoordinates, //uv channel 4
		float2(0,0), //uv channel 5
		float2(0,0), //uv channel 6
		float2(0,0)  //uv channel 7
	};

	UNROLL
	for( int CoordinateIndex = 0; CoordinateIndex < NUM_MATERIAL_TEXCOORDS_VERTEX; CoordinateIndex++ )
	{
		VertexParameters.TexCoords[CoordinateIndex] = UVArrays[CoordinateIndex];
	}

	#if HAS_MATERIAL_TEXCOORDS

		float2 CustomizedUVs[NUM_SLATE_TEXCOORDS];
		GetMaterialCustomizedUVs(VertexParameters, CustomizedUVs);

		UNROLL
		for( uint CoordinateIndex = 0; CoordinateIndex < NUM_SLATE_TEXCOORDS; CoordinateIndex++ )
		{
			FLATTEN
			if (Mod(CoordinateIndex, 2))
			{
				VOut.TextureCoordinates[CoordinateIndex / 2].zw = CustomizedUVs[CoordinateIndex];
			}
			else
			{
				VOut.TextureCoordinates[CoordinateIndex / 2].xy = CustomizedUVs[CoordinateIndex];
			}
		}
		
	#endif // HAS_MATERIAL_TEXCOORDS
#endif // NUM_MATERIAL_TEXCOORDS_VERTEX

#if HAS_SCREEN_POSITION
	WorldPosition.xyz = GetMaterialWorldPositionOffset(VertexParameters);
#endif

#endif // USE_MATERIALS

	VOut.Position = mul(WorldPosition, ViewProjection);

#if (ES2_PROFILE && COMPILER_GLSL_ES2) || (ES3_1_PROFILE && COMPILER_GLSL_ES3_1)
	// @todo-mobile: Fix this in the projection matrix
	VOut.Position.y *= SwitchVerticalAxisMultiplier;
#endif //(ES2_PROFILE && COMPILER_GLSL_ES2) || (ES3_1_PROFILE && COMPILER_GLSL_ES3_1)

	return VOut;
}
